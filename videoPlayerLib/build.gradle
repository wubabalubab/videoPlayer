plugins {
    id 'com.android.library'
}

android {
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {

    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    api ('com.google.guava:guava:' + "33.3.1-android") {
        // Exclude dependencies that are only used by Guava at compile time
        // (but declared as runtime deps) [internal b/168188131].
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'org.checkerframework', module: 'checker-compat-qual'
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }
}

task listDependencySize {
    doLast {
        // 选择可解析的 configuration
        def config = configurations.debugCompileClasspath

        // 收集所有 artifact
        def artifacts = []
        config.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            File file = artifact.file
            long sizeKB = file.length() / 1024
            artifacts << [name: "${artifact.moduleVersion.id.group}:${artifact.name}:${artifact.moduleVersion.id.version}", sizeKB: sizeKB, fileName: file.name]
        }

        // 按大小倒序排序
        artifacts.sort { -it.sizeKB }

        // 打印
        println "依赖大小列表（KB）倒序排序："
        artifacts.each { a ->
            println "${a.sizeKB} KB  ==> ${a.name} (${a.fileName})"
        }
    }
}

afterEvaluate {
    android.libraryVariants.all { variant ->
        if (variant.buildType.name == "release") {

            def versionName = android.defaultConfig.versionName ?: "1.0.0"
            def outDir = file("${rootProject.projectDir}/local-aar")
            def outName = "${project.name}-${versionName}-release.aar"

            variant.packageLibraryProvider.configure { task ->
                task.doLast {
                    copy {
                        from task.archiveFile.get().asFile
                        into outDir
                        rename { outName }
                    }
                    println(" Export AAR -> ${outDir}/${outName}")
                }
            }
        }
    }
}

